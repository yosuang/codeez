ext {
    moduleProjects = subprojects.findAll { it.name.startsWith("codeez-") }
    javaProjects = subprojects - project(":internal-platform") - project(":codeez-dependencies")
}

configure(allprojects) { project ->

    group = "com.yosuang.codeez"

    repositories {
        maven {
            credentials {
                username System.getenv("MAVEN_ALIYUN_USERNAME")
                password System.getenv("MAVEN_ALIYUN_PASSWORD")
            }
            url System.getenv("MAVEN_ALIYUN_REPOSITORY_BASE_URL") + System.getenv("MAVEN_ALIYUN_REPOSITORY_ID_RELEASE")
        }
        maven {
            credentials {
                username System.getenv("MAVEN_ALIYUN_USERNAME")
                password System.getenv("MAVEN_ALIYUN_PASSWORD")
            }
            url System.getenv("MAVEN_ALIYUN_REPOSITORY_BASE_URL") + System.getenv("MAVEN_ALIYUN_REPOSITORY_ID_SNAPSHOT")
        }
        maven {
            url 'https://maven.aliyun.com/repository/public'
        }
        mavenLocal()
        mavenCentral()
    }
}

configure([rootProject] + javaProjects) { project ->

    apply plugin: "java"

    configurations {
        dependencyManagement {
            canBeConsumed = false
            canBeResolved = false
            visible = false
        }
        matching { it.name.endsWith("Classpath") }.configureEach { it.extendsFrom(dependencyManagement) }
    }
    dependencies {
        dependencyManagement(enforcedPlatform(dependencies.project(path: ":internal-platform")))
        testImplementation(platform("org.junit:junit-bom:5.9.1"))
        testImplementation("org.junit.jupiter:junit-jupiter")
        implementation 'com.google.guava:guava:30.1-jre'
    }
}

configure(rootProject) {
    description = "Code Easy"
}
